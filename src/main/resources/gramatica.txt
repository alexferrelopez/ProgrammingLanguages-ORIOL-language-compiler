<A> ::= <funcs>
<funcs> ::= <func_type> <func_decl> <funcs> | ε
<func_type> ::= <data_type> DOS_PUNTS
<program> ::= MAIN <func_struct>
<func_decl> ::= <program> | <func_decl'>
<func_decl'> ::= VARIABLE <func_struct>
<func_struct> ::= <func_params> <func_body>
<func_params> ::= PO <param_list> PT
<param_list> ::= <param> <param_list'> | ε
<param_list'> ::= COMMA <param> <param_list'> | ε
<param> ::= <data_type> VARIABLE
<func_body> ::= CO <stmt_list> CT
<stmt_list> ::= <expression> <stmt_list> | ε
<return_stmt> ::= RETURN <return_stmt'> PUNT_COMMA
<return_stmt'> ::= <value> | VOID
<func_call> ::= VARIABLE PO <arg> PT PUNT_COMMA
<arg> ::= <value> <arg_list> | ε
<arg_list> ::= COMMA <value> <arg_list>  | ε
<declaration> ::= <data_type> <assignation> PUNT_COMMA | <assignation> PUNT_COMMA
<data_type> ::= INTEGER | FLOAT | CHAR | STRING | BOOLEAN | VOID
<assignation> ::= VARIABLE IS <assignation'>
<assignation'> ::= <arit_1> | <func_call> | <expr_bool>
<expression> ::= <declaration> | <condition> | <loop> | <func_call> | <return_stmt>
<value> ::= <literal_num> | <literal_bool> | VALUE_CHAR
<literal_num> ::= VALUE_FLOAT | VALUE_INT | VARIABLE
<expr_bool> ::=  <log_1> | <rela_1>
<literal_bool> ::= VALUE_TRUE | VALUE_FALSE
<condition> ::= IF <condition_expr> <condition_else>
<condition_expr> ::= PO <expr_bool> PT <func_body>
<condition_else> ::= ELSE <func_body> | ε
<loop> ::= <loop_for> | <loop_while>
<loop_for> ::= FOR PO <declaration> TO <literal_num> COMMA <assignation> PT <func_body>
<loop_while> ::= WHILE <condition_expr>
<log_1> ::= <log_2> <log_1'>
<log_1'> ::= OR <log_2> <log_1'> | ε
<log_2> ::= <log_3> <log_2'>
<log_2'> ::= AND <log_3> <log_2'> | ε
<log_3> ::= NOT <log_3'> <log_4>
<log_3'> ::= <log_3> | ε
<log_4> ::= PO <log_1> PT | <log_5>
<log_5> ::= <literal_bool> | VARIABLE
<arit_1> ::= <arit_2> <arit_1'>
<arit_1'> ::= SUM <arit_2> <arit_1'> | SUB <arit_2> <arit_1'> | ε
<arit_2> ::= <arit_3> <arit_2'>
<arit_2'> ::= DIV <arit_3> <arit_2'> | MUL <arit_3> <arit_2'> | MOD <arit_3> <arit_2'> | ε
<arit_3> ::= <arit_4> <arit_3'>
<arit_3'> ::= POW <arit_4> | ε
<arit_4> ::= PO <arit_1> PT | <arit_5>
<arit_5> ::= <literal_num>
<rela_1> ::= <rela_2> <rela_1'>
<rela_1'> ::= GT <rela_2> | LT <rela_2> | NEQ <rela_2> | EQ <rela_2> | ε
<rela_2> ::= PO <rela_1> PT | <value>
